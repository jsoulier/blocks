#include "shader.hlsl"

Texture2D<float> shadowTexture : register(t0, space0);
SamplerState shadowSampler : register(s0, space0);
Texture2D<float4> colorTexture : register(t1, space0);
Texture2D<float4> lightTexture : register(t2, space0);
Texture2D<float> ssaoTexture : register(t3, space0);
Texture2D<uint> voxelTexture : register(t4, space0);
Texture2D<float3> positionTexture : register(t5, space0);
[[vk::image_format("rgba8")]]
RWTexture2D<float4> compositeTexture : register(u0, space1);

cbuffer UniformBuffer : register(b0, space2)
{
    float4x4 ShadowTransform;
};

cbuffer UniformBuffer : register(b1, space2)
{
    float3 PlayerPosition;
};

// TODO: sky
static const float3 kAmbient = float3(0.5f, 0.5f, 0.5f);

static const float kSSAO = 3.0f;

[numthreads(8, 8, 1)]
void main(uint3 threadID : SV_DispatchThreadID)
{
    uint width;
    uint height;
    colorTexture.GetDimensions(width, height);
    if (threadID.x >= width || threadID.y >= height)
    {
        return;
    }
    float4 color = colorTexture[threadID.xy];
    float3 albedo = color.rgb;
    float alpha = color.a;
    uint voxel = voxelTexture[threadID.xy].x;
    float3 position = positionTexture[threadID.xy].xyz;
    float3 finalColor;
    float3 diffuse = lightTexture[threadID.xy].rgb;
    if (GetVoxelOcclusion(voxel))
    {
        float3 normal = GetVoxelNormal(voxel);
        float ssao = ssaoTexture[threadID.xy] * kSSAO;
        float shadow = GetShadow(shadowTexture, shadowSampler, ShadowTransform, position, normal);
        finalColor = albedo * (diffuse + kAmbient - ssao - shadow);
    }
    else if (voxel != 0)
    {
        // clouds?
        finalColor = albedo * (diffuse + kAmbient);
    }
    else
    {
        finalColor = albedo;
    }
    if (voxel != 0)
    {
        float3 skyColor = GetSkyColor(position - PlayerPosition);
        float fog = GetFog(distance(position.xz, PlayerPosition.xz));
        finalColor = lerp(finalColor, skyColor, fog);
    }
    compositeTexture[threadID.xy] = float4(finalColor, 1.0f);
}
