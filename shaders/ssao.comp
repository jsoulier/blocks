#include "voxel.hlsl"

Texture2D<uint> voxelTexture : register(t0, space0);
[[vk::image_format("r8")]]
RWTexture2D<float> ssaoTexture : register(u0, space1);

[numthreads(8, 8, 1)]
void main(uint3 threadID : SV_DispatchThreadID)
{
    uint width;
    uint height;
    ssaoTexture.GetDimensions(width, height);
    if (threadID.x >= width || threadID.y >= height)
    {
        return;
    }
    ssaoTexture[threadID.xy] = 0.0f;
    uint voxel = voxelTexture[threadID.xy];
    if (!VoxelIsValid(voxel))
    {
        return;
    }
    uint direction = VoxelGetDirection(voxel);
    int2 offsets[4] =
    {
        int2(-1,  0),
        int2( 1,  0),
        int2( 0, -1),
        int2( 0,  1)
    };
    float edge = 0.0f;
    for (int i = 0; i < 4; i++)
    {
        int2 id = threadID.xy + offsets[i];
        if (id.x < 0 || id.y < 0 || id.x >= width || id.y >= height)
        {
            continue;
        }
        uint neighbor = voxelTexture[id];
        if (!VoxelIsValid(neighbor))
        {
            continue;
        }
        if (direction != VoxelGetDirection(neighbor))
        {
            edge = 1.0f;
            break;
        }
    }
    ssaoTexture[threadID.xy] = edge;
}
